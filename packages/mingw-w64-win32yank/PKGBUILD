# Maintainer: Some One <some.one@some.email.com>

_realname=win32yank
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=0.1.1
pkgrel=1
pkgdesc="Windows clipboard tool"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
url='https://github.com/equalsraf/win32yank'
license=('ISC')
makedepends=("${MINGW_PACKAGE_PREFIX}-rust")
source=("${_realname}-${pkgver}.tar.gz::https://github.com/equalsraf/win32yank/archive/v${pkgver}.tar.gz")
sha256sums=('194126f114e3736e6e03bbf69ace9a2125ecfa2fc69bd1760d42fdd6adea1b37')

prepare() {
	cd "${srcdir}/${_realname}-${pkgver}"

	cargo fetch
}

build() {
	cd "${srcdir}/${_realname}-${pkgver}"

	cargo build --release --frozen
}

check() {
	cd "${srcdir}/${_realname}-${pkgver}"

	cargo test --release --frozen
}

package() {
	cd "${srcdir}/${_realname}-${pkgver}"

	cargo install \
		--offline \
		--no-track \
		--frozen \
		--path . \
		--root "${pkgdir}${MINGW_PREFIX}"

	install -Dm644 "${srcdir}/${_realname}-${pkgver}/LICENSE" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
